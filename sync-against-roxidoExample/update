#!/usr/bin/fish

while not test -f DESCRIPTION
  if test (pwd) = /
    echo "ERROR: Could not find DESCRIPTION file." >&2
    exit 1
  end
  cd ..
end

if string length -q -- (git status --untracked-files=no --porcelain)
  echo "WARNING: Git repository is not clean." >&2
end

set base ~/docs/devel/roxidoExample

for x in .gitignore \
         .Rbuildignore \
         configure \
         configure.win \
         DESCRIPTION \
         INSTALL \
         justfile \
         LICENSE \
         src/Makevars \
         src/Makevars.win \
         src/rust/build.rs \
         src/rust/Cargo.toml \
         src/rust/roxido/Cargo.toml \
         src/rust/roxido/src/lib.rs \
         src/rust/roxido/src/print.rs \
         src/rust/roxido/src/r.rs \
         src/rust/roxido/src/rbindings.rs \
         src/rust/roxido/src/stop.rs \
         src/rust/roxido_macro/Cargo.toml \
         src/rust/roxido_macro/src/lib.rs \
         src/rust/roxido_faer/Cargo.toml \
         src/rust/roxido_faer/src/lib.rs \
         tools/cargo_run.R \
         tools/rename_package.R
  set fullsrc $base/$x
  if not cmp --silent -- $fullsrc $x
    echo cp \"$fullsrc\" \"$PWD/$x\"
    meld $fullsrc $x
  end
end

